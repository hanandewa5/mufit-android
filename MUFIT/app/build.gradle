buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

def versionMajor = 1
def versionMinor = 8
def versionPatch = 0

android {
    lintOptions {
        abortOnError false
    }
    compileSdkVersion versions.compileSdk
    defaultConfig {
        applicationId "com.nostratech.mufit.consumer"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk

        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        setProperty("archivesBaseName", "MUFIT-v" + versionName)
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "MUFIT-v" + android.defaultConfig.versionName;
            if (variant.productFlavors[0].name.equals("development")) {
                outputFileName += "-developmentAPI";
            }
            if (variant.buildType.name == "release") {
                outputFileName += ".apk";
            } else {
                outputFileName += "-debug.apk";
            }
        }
    }

    signingConfigs {

        release {
            storeFile file('Mufit-Consumer')
            storePassword "Mufit4pp"
            keyAlias "Mufit-Consumer"
            keyPassword "Mufit4pp"
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "api"

    productFlavors {
        development {
            dimension "api"
            buildConfigField "String", "API_BASE_URL", "\"https://dev-api.mufit.id/\""
            buildConfigField "String", "API_BASE_URL2", "\"https://jsonplaceholder.typicode.com/\""
            buildConfigField "String", "MIDTRANS_CLIENT_KEY", "\"SB-Mid-client-uQOoc53AYvIyDCjR\""
            buildConfigField "String", "ADMIN_PAGE_BASE_URL", "\"https://dev-admin.mufit.id/\""
        }

        production {
            dimension "api"
            buildConfigField "String", "API_BASE_URL", "\"https://api.mufit.id/\""
            buildConfigField "String", "MIDTRANS_CLIENT_KEY", "\"Mid-client-q5QjvtfXm8QqrDwo\""
            buildConfigField "String", "ADMIN_PAGE_BASE_URL", "\"https://admin.mufit.id/\""
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

dependencies {

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //The bottomsheet project is a modified version of original bottomsheet, that adds the ability to customize font for menu items
    implementation project(path: ':bottomsheet')
    implementation project(path: ':bottomsheet-commons')
    //A single week calendar taken from materialcalendarview, that adds an empty week between 2 months in week view
    implementation project(path: ':singleweekcalendar')

    implementation 'com.gitlab.mufit-android-library:mufit-android-core-module:c6ffa0a'

    //Base Android libraries
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.0'
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    //Used to support <54k method count in <API 21 devices

    //Butterknife
    implementation "com.jakewharton:butterknife:$versions.butterknife"
    annotationProcessor "com.jakewharton:butterknife-compiler:$versions.butterknife"

    //HTTP & JSON Serialization
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$versions.retrofit"
    implementation 'com.google.code.gson:gson:2.8.5'

    //Rx Java and Android
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.9'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.1.1'
    implementation 'com.jakewharton.rxbinding2:rxbinding-design:2.1.1'

    //Testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'

    //UI libraries
    implementation 'de.hdodenhof:circleimageview:3.0.0'
    implementation 'com.makeramen:roundedimageview:2.3.0'
    implementation 'uk.co.chrisjenx:calligraphy:2.3.0'
    implementation 'com.github.prolificinteractive:material-calendarview:1.6.0'
    implementation 'com.synnapps:carouselview:0.1.5'
    implementation 'com.github.drawers:SpinnerDatePicker:1.0.6'
    implementation 'com.facebook.shimmer:shimmer:0.1.0@aar'
    implementation 'com.theartofdev.edmodo:android-image-cropper:2.7.0'
    implementation 'com.github.deano2390:MaterialShowcaseView:1.3.4@aar'
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
    implementation 'com.github.vipulasri:timelineview:1.1.3'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'

    //Utility Libraries
    implementation "com.github.bumptech.glide:glide:$versions.glide"
    annotationProcessor "com.github.bumptech.glide:compiler:$versions.glide"
    implementation 'id.zelory:compressor:2.1.0'
    implementation 'pub.devrel:easypermissions:3.0.0'

    //Google Libraries
//    implementation 'com.google.android.gms:play-services-places:15.0.1'
//    implementation 'com.google.android.gms:play-services-location:15.0.1'
    implementation 'com.google.android.libraries.places:places:1.1.0'
    implementation 'com.google.firebase:firebase-core:16.0.9'
    implementation 'com.google.firebase:firebase-messaging:18.0.0'


    implementation('com.crashlytics.sdk.android:crashlytics:2.9.4@aar') {
        transitive = true
    }
    implementation 'com.firebase:firebase-jobdispatcher:0.8.5'

    //MIDTRANS
    developmentImplementation "com.midtrans:uikit:$versions.midtrans-SANDBOX"
    productionImplementation "com.midtrans:uikit:$versions.midtrans"


}

apply plugin: 'com.google.gms.google-services'
